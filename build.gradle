plugins {
    id 'io.github.sebersole.quarkus-extension' version '0.9.3'
}

group = 'io.github.sebersole.quarkus'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

var quarkusVersion = "2.8.0.Final"

dependencies {
    quarkusPlatforms platform( "io.quarkus:quarkus-bom:${quarkusVersion}" )

    api "org.hibernate.orm:hibernate-core:6.0.0.Final"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

quarkusExtension {
    name = "Example Quarkus extension"
    description = "An example of using the `io.github.sebersole.quarkus-extension` plugin for defining a Quarkus extension"

    categories = [ "examples" ]
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        extension {
            pom {
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
            }
        }
    }
}

task showConfigurations {
    // just outputs some info about all of the defined Configurations so we can
    // see the ones added by the plugin for spi and deployment
    doLast {
        project.configurations.each { configuration ->
            logger.lifecycle "Configuration : {}", configuration.name
            logger.lifecycle "   - resolvable : {}", configuration.canBeResolved
            logger.lifecycle "   - consumable : {}", configuration.canBeConsumed
            logger.lifecycle "   - description : {}", configuration.description == null ? "{none}" : configuration.description
        }
    }
}